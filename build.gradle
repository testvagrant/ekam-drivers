plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
    mavenLocal()
}

sourceCompatibility = 1.8

dependencies {
    // Generic wait
    implementation('org.awaitility:awaitility:4.1.0')

    //appium
    implementation('io.appium:java-client:7.5.1')

    //Selenium
    implementation('org.seleniumhq.selenium:selenium-java:3.141.59')
    implementation('io.github.bonigarcia:webdrivermanager:4.4.3')

    //lombok
    implementation('org.projectlombok:lombok:1.18.20')
    annotationProcessor('org.projectlombok:lombok:1.18.20')
    testImplementation('org.projectlombok:lombok:1.18.20')
    testAnnotationProcessor('org.projectlombok:lombok:1.18.20')

    // Test Framework
    testImplementation('org.junit.jupiter:junit-jupiter-api:5.8.0-M1')
    testImplementation('org.junit.jupiter:junit-jupiter-engine:5.8.0-M1')
}

test {
    // Use TestNG for unit tests
    useJUnitPlatform()
}

task sourceJar(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives jar
    archives sourceJar
    archives javadocJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            customizePom(pom)
            groupId 'com.testvagrant.ekam'
            artifactId 'ekam-drivers'
            version '1.0.2'

            from components.java

            artifact(sourceJar) {
                classifier = 'sources'
            }
            artifact(javadocJar) {
                classifier = 'javadoc'
            }

        }
    }

    repositories {
        maven {
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username sonatypeUsername
                password sonatypePassword
            }
        }
    }
}

def customizePom(pom) {
    pom.withXml {
        def root = asNode()

        // eliminate test-scoped dependencies (no need in maven central POMs)
        root.dependencies.removeAll { dep ->
            dep.scope == "test"
        }

        // add all items necessary for maven central publication
        root.children().last() + {
            resolveStrategy = Closure.DELEGATE_FIRST

            description 'Ekam Drivers Project to create web and mobile drivers'
            name 'Ekam Drivers'
            url 'https://gitlab.com/testvagrant/automation-platform/ekam-studio/ekam-drivers'
            organization {
                name 'com.testvagrant.ekam'
                url 'https://gitlab.com/testvagrant/automation-platform/ekam-studio'
            }
            issueManagement {
                system 'GitLab'
                url 'https://gitlab.com/testvagrant/automation-platform/ekam-studio/ekam-drivers/issues'
            }
            licenses {
                license {
                    name 'MIT License'
                    url 'https://gitlab.com/testvagrant/automation-platform/ekam-studio/ekam-drivers/-/blob/master/LICENSE'
                    distribution 'repo'
                }
            }
            scm {
                url 'https://gitlab.com/testvagrant/automation-platform/ekam-studio/ekam-drivers'
                connection 'https://gitlab.com/testvagrant/automation-platform/ekam-studio/ekam-drivers.git'
                developerConnection 'git@gitlab.com:testvagrant/automation-platform/ekam-studio/ekam-drivers.git'
            }
            developers {
                developer {
                    name 'Sudarshan GS'
                }
                developer {
                    name 'Krishnanand B'
                }
            }
        }
    }
}

model {
    tasks.generatePomFileForMavenJavaPublication {
        destination = file("$buildDir/generated-pom.xml")
    }
}


signing {
    sign publishing.publications
}
